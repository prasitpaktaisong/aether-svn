bat_room,160,146,3	script	Esdras	804,{
	cutin "mets_alpha",2;
	mes "^0000FF[Esdras]^000000";
	mes "Olá, "+((Sex)?"meu caro ^0000FF":"minha cara ^FF0000")+strcharinfo(0)+"^000000.";
	mes "Em que posso te ajudar?";
	next;
	
	switch( select("• Receber o ^0000FFPacote de Maroll^000000","• Suprimentos de ^0000FFBatalhas Campais^000000","• Suprimentos de ^0000FFGuerra do Emperium^000000","• Cancelar")){
		
//== • Pacote de Maroll
		case 1:
			mes "^0000FF[Esdras]^000000";
			mes "É possível trocar os ^0000FFPontos de Participação^000000 das ^0000FFBatalhas Campais^000000 por um ^0000FFPacote de Maroll^000000.";
			mes "Permita-me verificar seus pontos.";
			next;

			mes "^0000FF[Esdras]^000000";
			mes "Tierra EoS: ^0000FF"+BG_EoE+"^000000 Pontos";
			mes "Tierra Boss: ^0000FF"+BG_Boss+"^000000 Pontos";
			mes "Tierra TI: ^0000FF"+BG_TI+"^000000 Pontos";
			mes "Flavius CTF: ^0000FF"+BG_CTF+"^000000 Pontos";
			mes "Flavius TDM: ^0000FF"+BG_TDM+"^000000 Pontos";
			mes "Flavius SC: ^0000FF"+BG_SC+"^000000 Pontos";
			mes "Conquest: ^0000FF"+BG_CON+"^000000 Pontos";
			mes "Rush: ^0000FF"+BG_RUSH+"^000000 Pontos";
			next;
			
			if(BG_EoE > 0 && BG_Boss > 0 && BG_CTF > 0 && BG_TDM > 0 && BG_SC > 0 && BG_TI > 0 && BG_CON > 0 && BG_RUSH > 0){
				set BG_EoE, BG_EoE - 1;
				set BG_Boss, BG_Boss - 1;
				set BG_TI, BG_TI - 1;
				set BG_CTF, BG_CTF - 1;
				set BG_TDM, BG_TDM - 1;
				set BG_SC, BG_SC - 1;
				set BG_CON, BG_CON - 1;
				set BG_RUSH, BG_RUSH - 1;
				getitem 20030,1;
				
				mes "^0000FF[Esdras]^000000";
				mes "Aqui está sua recompensa, espero que seja-lhe útil!";
				mes "Procure-me caso tenha mais ^0000FFPontos de Participação^000000.";
			} else {
				mes "^0000FF[Esdras]^000000";
				mes "Se deseja obter o ^0000FFPacote de Maroll^000000 precisa juntar ^0000FFPontos de Participação^000000 estando em cada um dos campos das ^0000FFBatalhas Campais^000000 ao menos uma vez!";
			}
			close2;
			cutin "",255;
			end;
		
//== • Suprimentos de Batalhas Campais
		case 2:
			setarray .@Items[0],   547,  504,  505, 7135, 7136, 7137, 7138, 7139,  678,  715,  716,  717,12016, 1025, 7321,  662;
			setarray .@Amount[0],  150,  100,  100,   40,   40,   30,   30,   25,   10,   50,  100,  200,   10,   40,   25,   10;
			setarray .@Value[0],     1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1,    1 ,   1,    1,    1;
			set .@cID, getbattleflag("bg_reserved_char_id");
			set .@Tarjet$, "Batalhas Campais";
			break;
	
//== • Suprimentos de Guerra
		case 3:
			setarray .@Items[0],  7137, 7138,12072,12077,12082,12087,12092,12097,14287,14288;
			setarray .@Amount[0],   30,   30,    6,    6,    6,    6,    6,    6,    5,    5;
			setarray .@Value[0],     4,    4,    8,    8,    8,    8,    8,    8,    8,    8;
			set .@cID, getbattleflag("woe_reserved_char_id");
			set .@Tarjet$, "Guerra do Emperium";
			break;
			
		case 4:
			mes "^0000FF[Esdras]^000000";
			mes "Aguardarei seu retorno, caso precise de meus serviços.";
			close2;
			cutin "",255;
			end;
	}

	set .@Menu$, "";
	for( set .@i, 0; .@i < getarraysize(.@Items); set .@i, .@i + 1 )
		set .@Menu$, .@Menu$+"• "+getitemname(.@Items[.@i])+" × "+.@Amount[.@i]+" (^0000FF"+.@Value[.@i]+"^000000 Emblemas):";
	set .@j, select(.@Menu$) - 1;

	mes "^0000FF[Esdras]^000000";
	mes "Adquirir:";
	mes "• ^0000FF"+getitemname(.@Items[.@j])+"^000000 × ^0000FF"+.@Amount[.@j]+"^000000";
	mes "Custará ^0000FF"+.@Value[.@j]+"^000000 Emblemas por ^0000FFConjunto^000000.";
	mes "Lembrando que só poderá ser utilizado em ^0000FF"+.@Tarjet$+"^000000.";
	next;
	
	mes "^0000FF[Esdras]^000000";
	mes "Onde deseja que sejam entregues os ^0000FFItens^000000 solicitados?";
	next;
	set .@Destiny, select("• Inventário:• Armazém") - 1;

	mes "^0000FF[Esdras]^000000";
	mes "Que quantidade, entre ^0000FF1^000000 e ^0000FF50^000000, deseja desse ^0000FFConjunto^000000?";
	mes "• ^0000FF"+getitemname(.@Items[.@j])+"^000000 × ^0000FF"+.@Amount[.@j]+"^000000";
	next;

	input .@total;
	if( .@total < 1 || .@total > 50 )
	{
		mes "^0000FF[Esdras]^000000";
		mes "Perdão, senhor"+((Sex)?"":"ita")+", a quantidade precisa ser entre ^0000FF1^000000 e ^0000FF50^000000.";
		mes "Fale comigo novamente quando você fizer suas escolhas.";
		close2;
		cutin "",255;
		end;
	}
	
	set .@vtotal, .@total * .@Value[.@j]; // Value of all Packages
	set .@total, .@total * .@Amount[.@j]; // Total Amount of Items

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID ) {
			if(checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0){
				mes "^0000FF[Esdras]^000000";
				mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, seu ^0000FFArmazém^000000 não possui lugares disponíveis o suficiente.";
				close2;
				cutin "",255;
				end;
			}
		} else {
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 ) {
				mes "^0000FF[Esdras]^000000";
				mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, seu ^0000FFArmazém^000000 não possui lugares disponíveis o suficiente.";
				close2;
				cutin "",255;
				end;
			}
		}
	}
	else
	{ // Check Weight Limit
		if( checkweight(.@Items[.@j],.@total) == 0 ){
			mes "^0000FF[Esdras]^000000";
			mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, você não pode carregar tanto peso.";
			close2;
			cutin "",255;
			end;
		}
	}

	if(countitem(7828) + countitem(7829) + (countitem(7773)/5) < .@vtotal ){
		mes "^0000FF[Esdras]^000000";
		mes "Para efetuar essa operação, são necessários ^0000FF"+.@vtotal+"^000000 Emblemas.";
		mes "Infelizmente você não possui essa quantidade.";
		close2;
		cutin "",255;
		end;
	}
	set .@vcount, .@vtotal;
	set .@i7828, 0;
	set .@i7829, 0;
	set .@i7773, 0;

	// Bravery Badges
	if(countitem(7828) > 0){
		mes "^0000FF[Esdras]^000000";
		mes "Emblemas Necessários: ^0000FF"+.@vcount+"^000000 Emblemas";
		mes "Quantos ^0000FFEmblemas de Bravura^000000 gostaria de utilizar?";
		next;
		input .@i7828;
		if(.@i7828 < 0 || .@i7828 > countitem(7828)){
			mes "^0000FF[Esdras]^000000";
			mes "Você não possui essa quantidade de ^0000FFEmblemas de Bravura^000000.";
			close2;
			cutin "",255;
			end;
		}
		
		if( .@i7828 > .@vcount ) set .@i7828,.@vcount;
		set .@vcount, .@vcount - .@i7828;
	}

	// Valor Badges
	if( .@vcount && countitem(7829) > 0 ){
		mes "^0000FF[Esdras]^000000";	
		mes "Emblemas Necessários: ^0000FF"+.@vcount+"^000000 Emblemas";
		mes "Quantos ^0000FFEmblemas de Valor^000000 gostaria de utilizar?";
		next;
		input .@i7829;
		if( .@i7829 < 0 || .@i7829 > countitem(7829)){
			mes "^0000FF[Esdras]^000000";
			mes "Você não possui essa quantidade de ^0000FFEmblemas de Bravura^000000.";
			close2;
			cutin "",255;
			end;
		}
		
		if( .@i7829 > .@vcount ) set .@i7829,.@vcount;
		set .@vcount, .@vcount - .@i7829;
	}

	// Heroism Badges
	if( .@vcount && countitem(7773) > 0 ){
		mes "^0000FF[Esdras]^000000";
		mes "Emblemas Necessários: ^0000FF"+.@vcount+"^000000 Emblemas";
		mes "Quantos ^0000FFEmblemas de Guerra^000000 gostaria de utilizar?";
		mes "Lembrando que ^0000FF5 Emblmas de Guerra^000000 são necessárias para diminuir ^0000FF1 Emblema Necessário^000000.";
		next;
		input .@i7773;
		if( (.@i7773 % 5) > 0 ) set .@i7773, .@i7773 - (.@i7773 % 5); // Value rounded down
		if( .@i7773 < 0 || .@i7773 > countitem(7773)){
			mes "^0000FF[Esdras]^000000";
			mes "Você não possui essa quantidade de ^0000FFEmblemas de Bravura^000000.";
			close2;
			cutin "",255;
			end;
		}

		if( .@i7773 > .@vcount * 5 ) set .@i7773, .@vcount * 5;
		set .@vcount, .@vcount - (.@i7773 / 5);
	}

	// Final Checks
	if( .@vcount != 0 )
	{
		mes "^0000FF[Esdras]^000000";
		mes "Perdão, senhor"+((Sex)?"":"ita")+", você não possui ^0000FFEmblemas^000000 o suficiente para o seu pedido.";
		close;
	}

	mes "^0000FF[Esdras]^000000";
	mes "Detalhes da negociação:";
	mes "• ^0000FFEmblemas da Bravura^000000 × ^0000FF"+.@i7828+"^000000 ";
	mes "• ^0000FFEmblemas do Valor^000000 × ^0000FF"+.@i7829+"^000000";
	mes "• ^0000FFEmblemas de Guerra^000000 × ^0000FF"+.@i7773+"^000000";
	mes "Confirmar?";
	next;

	if( select("• Sim:• Cancelar") == 2 )
	{
		mes "^0000FF[Esdras]^000000";
		mes "Aguardarei seu retorno, caso precise de meus serviços.";
		close2;
		cutin "",255;
		end;
	}

	if( .@Destiny )
	{ // Check Storage Space Limit
		if( .@cID ){
			if( checkspace(.@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff) == 0 ){
				mes "^0000FF[Esdras]^000000";
				mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, seu ^0000FFArmazém^000000 não possui lugares disponíveis o suficiente.";
				close2;
				cutin "",255;
				end;
			}
		} else {
			if( checkspace(.@Items[.@j],.@total,1,0,0,0,0,0,0) == 0 ){
				mes "^0000FF[Esdras]^000000";
				mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, seu ^0000FFArmazém^000000 não possui lugares disponíveis o suficiente.";
				close2;
				cutin "",255;
				end;
			}
		}
	} else { 
		if( checkweight(.@Items[.@j],.@total) == 0 ) {
			mes "^0000FF[Esdras]^000000";
			mes "Perdão, senhor"+((Sex)?"":"ita")+", aparentemente, você não pode carregar tanto peso.";
			close2;
			cutin "",255;
			end;
		}
	}

	if(.@i7828 > countitem(7828) || .@i7829 > countitem(7829) || .@i7773 > countitem(7773)){
		mes "^0000FF[Esdras]^000000";
		mes "O que?! Você é algum tipo de ^0000FFBruxo^000000 ou ^0000FFLadrão^000000? Onde estão seus ^0000FFEmblemas^000000?!";
		mes "Suma da minha frente, bastard"+((Sex)?"o":"a")+"!";
		close2;
	}

	delitem 7828,.@i7828;
	delitem 7829,.@i7829;
	delitem 7773,.@i7773;

	if(.@cID){
		if(.@Destiny) 
			storeitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
		else
			getitem2 .@Items[.@j],.@total,1,0,0,254,0,.@cID&0xffff,(.@cID>>16)&0xffff;
	} else {
		if( .@Destiny )
			storeitem .@Items[.@j],.@total;
		else
			getitem .@Items[.@j],.@total;
	}

	mes "^0000FF[Esdras]^000000";
	mes "Negociação completa!";
	mes "Volte caso precise de meus serviços novamente!";
	close2;
	cutin "",255;
	end;
}