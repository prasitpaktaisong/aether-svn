//== Aether Script ===========================================
// • NPCs de Izlude
//===== Por: =================================================
// • Haziel
//===== Versão Atual: ========================================
// • 1.0
//===== Descrição: ===========================================
// • NPCs Aether - Cidade de Izlude
//===== Changelog: ===========================================
// • 1.0 - Adicionado Michel - Lendárias Asas de Anjo e Demônio (04/07/2010)
//============================================================

//== • Edgar - Quest: Lendárias Asas de Anjo e Demônio ==================
izlude,116,222,3	script	Edgar, O Viajante#Izl	456,{
	if(AE_Asas >= 2){
		mes "^0000FF[Edgar]^000000";
		mes "Se procura pelas ^0000FFLendas Angelicais^000000, vá para ^0000FFYuno^000000.";
		mes "Se procura pelas ^FF0000Lendas Demoníacas^000000, vá para ^FF0000Morroc^000000.";
		mes "E você encontrará suas respostas.";
		close;
	}
					
	if(AE_Asas == 1){
		mes "^0000FF[Edgar]^000000";
		mes "Já conseguiu a ^0000FFFaixa de Cabelo^000000 que te pedi?";
		if((countitem(2210)) >= 1){
			switch(select("• Sim.","• Não.")){
				case 1:
					next;
					mes "^0000FF[Edgar]^000000";
					mes "Oh, muito bem. Esperava que você conseguisse.";
					delitem 2210,1;
					next;

					mes "^0000FF[Edgar]^000000";
					mes "Saiba antes de tudo, a base do desenvolvimento de qualquer essência está no equilíbrio e, para isso, você precisa conhecer os dois lados.";
					next;
					
					mes "^0000FF[Edgar]^000000";
					mes "Se procura pelas ^0000FFLendas Angelicais^000000, vá para ^0000FFYuno^000000.";
					mes "Se procura pelas ^FF0000Lendas Demoníacas^000000, vá para ^FF0000Morroc^000000.";
					mes "E você encontrará suas respostas.";
					set AE_Asas,2;
					close;
	
				case 2:
					close;
			}
			close;
		}
	}

	mes "^0000FF[Edgar]^000000";
	mes "Os mortais...";
	mes "Pensam que suas vidas estão fadadas em escolher apenas entre dois lados...";
	next;
	
	mes "^0000FF[Edgar]^000000";
	mes "A verdadeira essência é o equilíbrio...";
	switch(select("• Perguntar sobre os 'dois lados'.","• Não fazer nada.")){
		case 1:
			next;
			mes "^0000FF[Edgar]^000000";
			mes "Uh...";
			mes "Curioso sobre o assunto?";
			mes "Bom, se deseja saber a diferença entre os dois lados e o que cada um pode te oferecer, posso te mostrar uma parcela disso.";
			next;

			mes "^0000FF[Edgar]^000000";
			mes "Mas, para isso, quero uma ^0000FFFaixa de Cabelo^000000. Será que tu consegues uma?";
			set AE_Asas,1;
			close;

		case 2:
			close;
	}
}

