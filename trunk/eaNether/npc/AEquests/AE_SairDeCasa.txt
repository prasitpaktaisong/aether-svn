//== • Catarina - Quest: Sair de Casa =====================================
new_1-1,51,112,5	script	Catarina#apr	882,{
	if(AE_SairDeCasa > 0){
		mes "^FF0000[Catarina]^000000";
		mes "Oh, boa sorte então.";
		close;
	}
	mes "^FF0000[Catarina]^000000";
	mes "Oi, "+((Sex)?"irmãozinho":"irmãzinha")+".";
	mes "Mamãe está te chamando para vir para casa, você vem?";
	switch(select("• Sim","• Não")){
		case 1:
			next;
			mes "^FF0000[Catarina]^000000";
			mes "Te vejo lá em casa!";
			close2;
			warp "grandma",183,97;
			end;
			
		case 2:	
			mes "^FF0000[Catarina]^000000";
			mes "Oh, boa sorte então.";
			set AE_SairDeCasa,16;
			close;
	}
}

//== • Catarina - Quest: Sair de Casa =====================================
grandma,182,96,8	script	Catarina#gra	882,{
	if(AE_SairDeCasa < 1){
		mes "^FF0000[Catarina]^000000";
		mes "Bom dia, "+((Sex)?"^0000FF":"^FF0000")+""+strcharinfo(0)+"!^000000";
		mes "Que bom que você acordou, mamãe deixou um bilhete para você no ^3355FFQuadro de Avisos^000000 ali do lado.";
		mes "Leia-o por favor.";
		set AE_SairDeCasa,1;
		close;
	}
	mes "^FF0000[Catarina]^000000";
	mes "Mamãe deixou um bilhete para você no ^3355FFQuadro de Avisos^000000 ali ao lado.";
	close;
}

//== • Guarda-Roupas - Quest: Sair de Casa ================================
grandma,184,91,1	script	Guarda-Roupas#gra	111,{
	if(AE_SairDeCasa > 1){
		mes "["+strcharinfo(0)+"]";
		mes "^3355FFAgora vou ler o bilhete no quadro!^000000";
		close;
	}
	if(AE_SairDeCasa == 1){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "^3355FFÉ melhor eu me vestir antes de sair.^000000";
		set AE_SairDeCasa,2;
		getitem 12323, 30; //Novice Fly Wing
		getitem 2112, 1; //Novice Guard
		getitem 12325, 10; //Novice Magnifier
		getitem 1243, 1; //Novice_Rod
		getitem 2414, 1; //Novice Slippers
		getitem 569, 350; //Novice Potion
		getitem 2352, 1; //Tattered Novice Ninja Suit
		getitem 2510, 1; //Somber Novice Hood
		next;
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Agora vou ler o bilhete no quadro!";

		close;
	}
}

//== • Quadro de Avisos - Quest: Sair de Casa =============================
grandma,176,97,6	script	Quadro de Avisos#gra	857,{
	if(AE_SairDeCasa > 2){
		dispbottom "*Você recebeu a Chave Dourada, use-a para abrir a Porta do quarto.*";
		close;
	}
	if(AE_SairDeCasa == 2){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "O que devo fazer?";
		mes "^FF0000(Responda com cuidado!)";
		mes	"(Você não poderá voltar atrás!)^000000";
		switch(select("• Ler o ^0000FFBilhete^000000.","• Fugir de Casa.")){
			case 1:	
				next;
				mes "^0000FF[Bilhete]^000000";
				mes "^3355FFBom dia meu anjo!";
				mes "Dormiu bem? Espero que sim! Eu fiquei sabendo por um pássarinho verde que papai noel deixou um presentinho para você em nossa árvore de natal. Se eu fosse você correria, abriria a porta e iria ver o que é. Desça para tomar café quando terminar, amamos você, Papai, Mamãe e Catrii.^000000";
				set AE_SairDeCasa,3;
				close2;
				getitem 7876,1;
				dispbottom "*Você recebeu a Chave Dourada, use-a para abrir a Porta do quarto.*";
				end;
				
			case 2:
				next;
				mes "^0000FF[Bilhete]^000000";
				mes "Não sei qual o motivo que o levou a sair de casa. Creio que você já deve estar crescido, ou brigou comigo ou seu pai.";
				next;

				mes "^0000FF[Bilhete]^000000";
				mes "O motivo tanto faz. Espero que você seja muito feliz, há algumas coisas sobre o mundo lá fora que você precisa saber, não posso lhe contar tudo em apenas uma carta mas tentarei resumir o máximo para ti. Faça uma boa leitura das 3 coisas que você deve se lembrar ao longo da vida";
				next;

				mes "^0000FF[Bilhete]^000000";
				mes "^0000FF1ª^000000: A senha de sua ^0000FFConta^000000 é de responsabilidade ^FF0000sua^000000, portanto seja o único a possuí-la! A ^0000FFRealeza^000000 declara-se no direito de isentar-se de qualquer responsabilidade, dependendo do caso.";
				next;

				mes "^0000FF[Bilhete]^000000";
				mes "^0000FF2ª^000000: Lembre-se da educação que eu te dei. E no mundo é proibido o uso abusivo de: termos de baixo calão, conteúdo obsceno, sexualmente explícito, moralmente ofensivo, termos moralmente preconceituosos, ilegais, etnicamente ofensivos, religiosamente preconceituosos, sexualmente preconceituosos ou odiosos. O mesmo vale para termos e siglas que possam ser interpretados dessa forma pelo senso comum e pela comunidade.";
				next;

				mes "^0000FF[Bilhete]^000000";
				mes "^0000FF3ª^000000: Filho, lembre-se sempre de gritar bem alto: ^0000FF@noks^000000 todos os dias para evitar que espertinhos roubem seus monstros. Neste mundo a gente vê tanta coisa errada. Você também não deve fazer coisas erradas como usar suas habilidades para atrapalhar outras pessoas, usar trapassas para conseguir as coisas mais rápido na vida, abusar de coisas erradas, mentir sobre denúncias, e principalmente perseguir pessoas que não gosta, deixe-as em paz, e se alguém fizer algo que o faça sentir lesado, denúncie.";
				next;
				
				mes "^0000FF[Bilhete]^000000";
				mes "Eu estou tão emocionada, tomara que não seja a ultima vez que escreva para ti, peço escreva de volta dando notícias.";
				next;

				mes "^0000FF[Bilhete]^000000";
				mes "Com carinho daquela que te ama muito.";
				mes "";
				mes "Mamãe.";
				set AE_SairDeCasa,0;
				next;

				warp "prt_in",65,141;
				savepoint "prt_in",65,141;
				close;
		}
	}	
	if(AE_SairDeCasa == 1){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "^3355FF*Irei no guarda-roupas me vestir antes de ler, está muito frio!*^000000";
		close;
	}
}

//== • Porta - Quest: Sair de Casa ========================================
grandma,178,100,1	script	Porta#gra	111,{
	if(AE_SairDeCasa > 3){
		dispbottom "*Não é hora de voltar ao quarto.*";
	}
	if(AE_SairDeCasa == 3){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Devo usar a chave?";
		switch(select("• Sim","• Não")){
			case 1:
				mes "Você usou a sua ^3355FFchave^000000.";
				delitem 7876,1;
				set AE_SairDeCasa,4;
				warp "grandma",178,101;
				close;
			
			case 2:
				close;
		}
	}		
}

//== • Presente - Quest: Sair de Casa =====================================
grandma,180,117,1	script	Presente#gra	111,{

	if(AE_SairDeCasa > 4){
		dispbottom "*O Presente já foi aberto, agora é hora de ir falar com Mamãe*";
		end;
	}
	if(AE_SairDeCasa == 4){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Abro o ^0000FFPresente^000000?";
		switch(select("• Sim.","• Não")){
			case 1:
		
				set AE_SairDeCasa,5;
				getitem 14533,1; 
				set BaseLevel,BaseLevel+1;
				set JobLevel,JobLevel+1;
				next;
				mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
				mes "Agora é melhor descer e procurar a ^0000FFMamãe^000000.";				close;
			
			case 2:
				close;
		}
	}	
}

//== • Saida - Escadas - Quest: Sair de Casa ==============================
grandma,175,116,1	script	Saida	45,1,1,{
	if(AE_SairDeCasa < 5){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Ah! Quase me esqueci de abrir meu presente na árvore de natal, é melhor voltar e pega-lo.";
		close;
	}
	warp "grandma",173,41;
	close;
}

//== • Mamãe - Quest: Sair de Casa ========================================
grandma,165,38,3	script	Mamãe#gra	885,{
	if(AE_SairDeCasa == 16){
		mes "^FF0000[Arwen Heart]^000000";
		mes "Agora você pode ir, querid"+((Sex)?"o":"a")+".";
		close;
	}
	if(AE_SairDeCasa == 15){
		mes "^FF0000[Arwen Heart]^000000";
		mes "Meu filho, você deve estar ansioso para ir explorar o mundo. Para te ajudar, leve estas poções, boa sorte meu anjo!";
		set AE_SairDeCasa,16;
		getitem 501,50; //RedPotion
		getitem 503,10; //YellowPotion
		set zeny,zeny+5000;
		set BaseLevel,BaseLevel+1;
		set JobLevel,JobLevel+1;
		close;
	}	
	if(AE_SairDeCasa > 7){
		mes "^FF0000[Arwen Heart]^000000";
		mes "Oh! Seu pai está te chamando, vá ver do que ele precisa.^000000";
		close;
	}
	if(AE_SairDeCasa > 5){
		mes "^FF0000[Arwen Heart]^000000";
		mes "Deixe-me ver se está tudo certo.";
		next;

		mes "^FF0000[Arwen Heart]^000000";
		mes "Hum...";
		if(countitem(574) < 2){
			next;
			mes "^FF0000[Arwen Heart]^000000";
			mes "Você ainda não tem todo os ovos, quando pega-los, volte até mim.";
			close;
		}
		next;
		
		mes "^FF0000[Arwen Heart]^000000";
		mes "Ótimo, perfeitos!";
		mes "Me entregue esses ovos.";
		delitem 574,2;
		next;
		
		mes "^FF0000[Arwen Heart]^000000";
		mes "Obrigada, estou preparando seu café, já já está pronto.";
		next;
		
		emotion e_dots,1;	
		mes "^FF0000[Arwen Heart]^000000";
		mes "Tome aqui seu café da manhã.";
		set BaseLevel,BaseLevel+2;
		set JobLevel,JobLevel+2;
		next;

		mes "^FF0000[Arwen Heart]^000000";
		mes "Oh! Seu pai está te chamando, vá ver do que ele precisa.^000000";
		set AE_SairDeCasa,8;
		close;
	}
	if(AE_SairDeCasa == 5){	
		mes "^FF0000[Arwen Heart]^000000";
		mes "Bom dia, "+((Sex)?"^0000FF":"^FF0000")+""+strcharinfo(0)+"^000000!";
		mes "Estou preparando seu café, você poderia me ajudar?";
		switch(select("• Sim")){
			case 1:
				next;
				mes "^FF0000[Arwen Heart]^000000";
				mes "Por favor meu filho vá até o quintal ^3355FFnos fundos da casa^000000 e pegue ^3355FF2 ovos^000000 na galinha.";
				set AE_SairDeCasa,6;
				close;
		}
	}
}

//== • Galinha - Quest: Sair de Casa ======================================
grandma,36,103,3	script	Galinha#gra	800,{

OnTouch:
	set @ovo, rand(1,100);
	if ( @ovo < 86 ) {
		dispbottom "*A Galinha te bicou!*";
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "AWCH!";
		specialeffect2 5;
		percentheal -20,0;
		savepoint "grandma",164,34;
		close;
	}
	if ( @ovo > 85 ) {
		if(AE_SairDeCasa == 6){
			set AE_SairDeCasa,7;
			getitem 574,2;
			set BaseLevel,BaseLevel+1;
			set JobLevel,JobLevel+1;
			set PegouOvos,1;
		}
		dispbottom "*A Galinha te bicou!*";
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "AWCH!";
		specialeffect2 5;
		percentheal -20,0;
		savepoint "grandma",164,34;
		close;
	}
}

//== • Papai - Quest: Sair de Casa ========================================
grandma,64,101,3	script	Papai#gra	459,{
	if(AE_SairDeCasa == 11){
		mes "^0000FF[Cold Heart]^000000";
		mes "Obrigado, "+((Sex)?"meu filho":"minha filha")+". Agora vá escovar os dentes!";
		close;
	}
	if(AE_SairDeCasa > 8){
		mes "^0000FF[Cold Heart]^000000";
		mes "Obrigado, "+((Sex)?"meu filho":"minha filha")+", pegue então a ^3355FFAgua^000000 do ^3355FFPoço ao Lado^000000 e vá até a ^3355FFAbóbora^000000 regar!";
		close;
	}
	if(AE_SairDeCasa == 8){
		mes "^0000FF[Cold Heart]^000000";
		mes "Filho preciso que você me ajude a regar as plantas.";
		next;

		mes "^0000FF[Cold Heart]^000000";
		mes "Só está faltando a ^3355FFAbóbora^000000, você poderia rega-la para mim?";
		switch(select("• Sim")){
			case 1:
				next;
				mes "^0000FF[Cold Heart]^000000";
				mes "Obrigado, "+((Sex)?"meu filho":"minha filha")+", pegue então a ^3355FFAgua^000000 do ^3355FFPoço ao Lado^000000 e vá até a ^3355FFAbóbora^000000 regar!";
				set AE_SairDeCasa,9;
				close;
		}
	}
}

//== • Poço - Quest: Sair de Casa =========================================
grandma,66,99,1	script	Poço#gra	111,{
	if(AE_SairDeCasa > 9){
		dispbottom "*Você recolheu a Água para regar a Abóbora.*";
		end;
	}	
	if(AE_SairDeCasa == 9){
		dispbottom "*Você recolhe a Água para regar a Abóbora.*";
		set AE_SairDeCasa,10;
		getitem 720,1;
		end;
	}
}

//== • Abóbora - Quest: Sair de Casa ======================================
grandma,49,104,1	script	Abobora#gra	111,{

OnTouch:
	if(AE_SairDeCasa > 10){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Já reguei a Abóbora. Agora preciso ir escovar os dentes!";
		close;
	}
	if(AE_SairDeCasa == 10){
		if(countitem(720) > 0){
			dispbottom "A Abóbora foi regada!";
			set AE_SairDeCasa,11;
			set BaseLevel,BaseLevel+1;
			set JobLevel,JobLevel+1;
			delitem 720, 1;
		
			mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
			mes "Agora preciso ir escovar os dentes!";
			close;
		}
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Preciso de água para regar!";
		close;
				
	}	
}

//== • Pia - Quest: Sair de Casa ==========================================
grandma,95,98,1	script	Pia#gra	111,{

OnTouch:
	if(AE_SairDeCasa > 11){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Já escovei os dentes. ^FF0000Vovó^000000 está chamando. Melhor ver o que ela quer na sala.";
		close;
	}
	if(AE_SairDeCasa == 11){
		dispbottom "*Você escovou os dentes*";
		set AE_SairDeCasa,12;
		set BaseLevel,BaseLevel+1;
		set JobLevel,JobLevel+1;
		mes "^FF0000[Cici Heart]^000000";
		mes ((Sex)?"^0000FF":"^FF0000")+""+strcharinfo(0)+", venha cá, menin"+((Sex)?"o":"a")+"!";
		next;
		
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Oh, ^FF0000Vovó^000000 está chamando. Melhor ver o que ela quer na sala.";
		close;
	}
}

//== • Saida - Banheiro - Quest: Sair de Casa =============================
grandma,101,91,1	script	Saida#Banheiro	45,1,1,{

OnTouch:
	if(AE_SairDeCasa == 11){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Preciso escovar os dentes!";
		close;
	}
	warp "grandma",144,19;	
}

//== • Vovó Cici - Banheiro - Quest: Sair de Casa =========================
grandma,164,21,5	script	Vovó Cici#gra	840,{
	if(AE_SairDeCasa > 14){
		mes "^FF0000[Vovó Cici]^000000";
		mes "Sua mãe quer falar com você, vá procurá-la!";
		close;
	}
	if(AE_SairDeCasa == 14){
		mes "^FF0000[Vovó Cici]^000000";
		mes "Você estudou o Livro, "+((Sex)?"meu neto":"minha neta")+"?";
		switch(select("• Sim","• Não")){
			case 1:
				next;
				mes "^FF0000[Vovó Cici]^000000";
				mes "Então, vamos ver se acerta minha pergunta.";
				next;
				
				mes "^FF0000[Vovó Cici]^000000";
				mes "Qual desses ingredientes ^FF0000não^000000 é usado nas receitas do livro que você acabou de ler?";
				switch(select("• Pernas de Gafanhoto","• Mel","• Morango","• Oléo de Cozinha","• Cancelar")){
					case 1:
						mes "^FF0000[Vovó Cici]^000000";
						mes "Resposta errada! Volte a estudar!";
						close;
						
					case 2:
						mes "^FF0000[Vovó Cici]^000000";
						mes "Resposta errada! Volte a estudar!";
						close;
					
					case 3:
						next;
						mes "^FF0000[Vovó Cici]^000000";
						mes "Parabéns, "+((Sex)?"meu neto":"minha neta")+"!";
						set AE_SairDeCasa,15;
						set BaseLevel,BaseLevel+1;
						set JobLevel,JobLevel+1;
						next;
						
						mes "^FF0000[Vovó Cici]^000000";
						mes "Sua mãe quer falar com você, vá procurá-la!";
						close;
					
					case 4:
						mes "^FF0000[Vovó Cici]^000000";
						mes "Resposta errada! Volte a estudar!";
						close;
					
					case 5:
						mes "^FF0000[Vovó Cici]^000000";
						mes "Então volte a estudar!";
						close;

				}
			case 2:
				close;
		}
	}
	if(AE_SairDeCasa == 13){
		mes "^FF0000[Vovó Cici]^000000";
		mes "Bem, primeiramente você precisa pegar 1 livro que está embaixo do fogão, pegue-o e leia com atenção.";
		next;

		mes "^FF0000[Vovó Cici]^000000";
		mes "Quando pegar volte até mim.";
		close;
	}
	if(AE_SairDeCasa == 12){		
		mes "^FF0000[Vovó Cici]^000000";
		mes "Olá, "+((Sex)?"^0000FF":"^FF0000")+""+strcharinfo(0)+"^000000.";
		mes "Você gostaria de aprender mais sobre culinária?";
		switch(select("• Sim")){
			case 1:
				next;
				mes "^FF0000[Vovó Cici]^000000";
				mes "Bem, primeiramente, você precisa pegar 1 livro que está debaixo do fogão, pegue-o e leia com atenção.";
				next;
				mes "^FF0000[Vovó Cici]^000000";
				mes "Quando pegar volte até mim.";
				set AE_SairDeCasa,13;
				close;
		}
	}		
}

//== • Livro - Fogão - Quest: Sair de Casa ================================
grandma,159,44,1	script	Livro#1	111,1,1,{

OnTouch:
	if(AE_SairDeCasa > 13){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Melhor voltar a falar com a vovó.";
		close;
	}	
	if(AE_SairDeCasa == 13){
		mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
		mes "Pego o livro?";
		switch(select("• Sim")){
			set AE_SairDeCasa,14;
			set BaseLevel,BaseLevel+1;
			set JobLevel,JobLevel+1;
			getitem 7472,1;
			next;

			mes ((Sex)?"^0000FF":"^FF0000")+"["+strcharinfo(0)+"]^000000";
			mes "Melhor voltar a falar com a vovó.";
			close;
		}
	}	
}

//== • Saída - Casa - Quest: Sair de Casa =================================
grandma,129,35,1	script	Saída#Tutorial	45,1,1,{
	if(AE_SairDeCasa == 16){
		savepoint "prontera",238,43;
		warp "grandma",18,114;
	}
	if(AE_SairDeCasa < 16){
		dispbottom "*Você ainda não pode sair da casa!*";
		end;
	}
}

grandma,21,114,0	warp	Entrada#DoTuto	2,2,grandma,134,35
grandma,10,113,0	warp	Entrada#CasaEscola	2,2,prontera,238,43
grandma,183,35,0	warp	Entrada#CasaQuintal	2,2,grandma,34,120
grandma,29,120,0	warp	Entrada#QuintalCasa	2,2,grandma,180,36
grandma,141,19,0	warp	Entrada#banheiro	2,2,grandma,100,94