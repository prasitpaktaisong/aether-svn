vip,73,42,3	script	Caça-Níqueis Poring	563,{
	mes "^0000FF[Caça-Níqueis Poring]^000000";
	mes "Gostaria de apostar, "+((Sex)?"senhor ^0000FF":"senhora ^FF0000")+strcharinfo(0)+"^000000?";
	next;
	
	switch(select("• Apostar","• Informações","• Cancelar")){
		case 1:
			set @qtmoedas,0;
			set @apostadas,0;
			set @base,0;
			set @premio,0;
			mes "^0000FF[Caça-Níqueis Poring]^000000";
			if(countitem(20004) < 1){
				mes "Para apostar, é necessário, no mínimo ^0000FF1^000000 e no máximo ^0000FF3^000000 Moedas de Freya.";
				close;
			}
			mes "Quantas ^0000FFMoedas de Freya^000000 deseja apostar?";
			mes "É necessário pelo menos ^0000FF1^000000 e permitidas até ^0000FF3^000000, dando-lhe direito à ^0000FF3^000000 tentativas independente da quantidade.";
			next;
			input @qtmoedas;
			
			if(@qtmoedas < 1 || @qtmoedas > 3){
				mes "^0000FF[Caça-Níqueis Poring]^000000";
				mes "Você não apostou ^0000FFMoeda de Freya^000000 na quantidade certa e não poderá jogar.";
				mes "São permitidos apenas entre ^0000FF1^000000 e ^0000FF3^000000 Moedas de Freya!";
				close;
			}
			
			delitem 20004,@qtmoedas;
			set @tentativa,3;
			mes "^0000FF[Caça-Níqueis Poring]^000000";
			mes "Você apostou ^0000FF"+@qtmoedas+"^000000 Moedas de Freya";
			
			for (set @apostadas, 0; @apostadas < @qtmoedas; set @apostadas, @apostadas + 1){
				set @base, rand(250000,500000);
				if(@qtmoedas == 1){
					mes "O valor base da ^0000FFMoeda de Freya^000000 apostada é: ^0000FF"+@base+"^000000!";
					set @premio, @premio + @base;
				} else {
					mes "O ^0000FF"+(@apostadas + 1)+"^000000º Valor-base é: ^0000FF"+@base+"^000000!";
					set @premio, @premio + @base;
				}
			}
			if(@qtmoedas > 1){
				mes "Que somam ^0000FF"+@premio+"^000000!";
			}
			next;

Jogo:			
			mes "^0000FF[Caça-Níqueis Poring]^000000";
			mes "Puxe a alavanca para começar a jogar.";
			switch(select("• Puxar Alavanca")){
				case 1:
					set @slot1, rand(1,7);
					mes "Primeiro número é ^0000FF"+@slot1+"^000000!";
					switch(select("• Puxar Alavanca")){
						case 1:
							set @slot2, rand(1,7);
							mes "Segundo número é ^0000FF"+@slot2+"^000000!";
							switch(select("• Puxar Alavanca")){
								case 1:
									set @slot3, rand(1,7);
									mes "Terceiro número é ^0000FF"+@slot3+"^000000!";
									next;

									if((@slot1 == @slot2) && (@slot2 == @slot3)){
										specialeffect2 10;
										mes "^0000FF[Caça-Níqueis Poring]^000000";
										mes "Você acertou ^0000FF3^000000 vezes o número ^0000FF"+@slot1+"^000000.";
										mes "Seu prêmio total é ^0000FF"+@premio+"^000000 × ^0000FF"+@slot1+"^000000.";
										mes "Somando: ^0000FF"+(@premio*@slot1)+"^000000.";
										set Zeny, Zeny + (@premio*@slot1);
										next;
									}
									
									set @tentativa, @tentativa - 1;
									if(@tentativa > 0){
										mes "^0000FF[Caça-Níqueis Poring]^000000";
										mes "Sua aposta ainda garantem-lhe ^0000FF"+@tentativa+"^000000 tentativas.";
										mes "Deseja continuar?";
										next;
										switch(select("• Sim","• Não")){
											case 1:
												goto Jogo;
											case 2:
												close;
										}
									}
									mes "^0000FF[Caça-Níqueis Poring]^000000";
									mes "O Jogo a partir desta aposta está encerrado.";
									mes "Volte sempre!";
									close;
							}
					
					}	
			}
	
		case 2:
			mes "^0000FF[Caça-Níqueis Poring]^000000";
			mes "A mecânica do ^0000FFCaça-Níqueis Poring^000000 é muito simples.";
			mes "O objetivo é, com sorte, obter o mesmo número ^0000FF3^000000 vezes consecutivas.";
			mes "Os números variam de ^0000FF1^000000 à ^0000FF7^000000";
			next;
			
			mes "^0000FF[Caça-Níqueis Poring]^000000";
			mes "Para apostar, é necessário, no mínimo ^0000FF1^000000 e no máximo ^0000FF3^000000 Moedas de Freya.";
			mes "Cada ^0000FFMoeda de Freya^000000 soma o valor base que, para cada Moeda, varia entre ^0000FF250.000^000000 e ^0000FF500.000^000000z";
			next;

			mes "^0000FF[Caça-Níqueis Poring]^000000";
			mes "O valor da premiação é o valor obtido três vezes multiplicado pelo valor base da aposta.";
			mes "São permitidas ^0000FF3^000000 tentativas por ^FF0000Aposta^000000, não por ^0000FFMoeda^000000!";
			close;
			
		case 3:
			close;
	}
}