//== Aether Script ===========================================
// • Chapéu de Mosqueteiro
//===== Script: ==============================================
// • Ciel
//===== História: ==============================================
// • Aelitha
//===== Versão Atual: ========================================
// • 1.0
//===== Descrição: ===========================================
// • NPCs Aether - Quest do Chapéu de Mosqueteiro
//===== Changelog: ===========================================
// • 1.0 - Criação (Ciel)
//============================================================

//== • Hietalla, O Pesquisador - Quest: Chapéu de Mosqueteiro =============
prt_castle,78,50,7	script	Hietalla, O Pesquisador	47,{
	if(AE_NobleHat == 0){ goto Start; }
	if(AE_NobleHat == 1){ goto Step1; }
	if(AE_NobleHat == 2){ goto Step2; }
	if(AE_NobleHat == 3){ goto Step3; }
	if(AE_NobleHat == 4){ goto Step4; }
	if(AE_NobleHat == 5){ goto Step5; }
	if(AE_NobleHat == 6){ goto Step6; }
	
Start:
	mes "^696969-Esse homem parece um pouco preocupado-^000000";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Oi, você está bem?";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Ah, sim! Estou bem...";
	mes "Apenas tentando lembrar uma antiga história...";
	switch(select("• Ah!! Desculpe-me por atrapalhar.","• Perdão, mas pode me contar o que lembra?")){
		case 1:
		next;
		mes "^0000FF[Hietalla]^000000";
		mes "Sem problemas.";
		close;
		
		case 2:
		next;
		mes "^0000FF[Hietalla]^000000";
		mes "'... E eram três a cavalgar. ^0000FFO Sobrevivente^000000, ^FFA500O Bondoso^000000 e ^CD2626A Lança dos Deuses^000000...'";
		mes "^696969-O homem olha em seus olhos-^000000";
		mes "Desculpe... mas é só isso que lembro.";
		next;
		mes "^BF3EFF["+strcharinfo(0)+"]^000000";
		mes "Que importância tem essa história?";
		next;
		mes "^0000FF[Hietalla]^000000";
		mes "Ela fala dos três guerreiros que protegiam a realeza...";
		mes "Mas quando Prontera ficou sem Rei, eles perderam seus cargos.";
		switch(select("• Que pena. Deveria ser uma boa história.","• Posso te ajudar a recuperar a história?")){
			case 1:
			next;
			mes "^0000FF[Hietalla]^000000";
			mes "Sim.. espero, um dia, lembrar dela por completo!";
			close;
			
			case 2:
			next;
			mes "^0000FF[Hietalla]^000000";
			mes "HO! SÉRIO?! Muito obrigado!!";
			mes "Jovem... mais uma coisa...";
			mes "Leve algo para que eles lembrem de quem são.";
			next;
			mes "^0000FF[Hietalla]^000000";
			mes "Procure por um item chamado:";
			mes "• ^FF0000Insígnia da Cavalaria^000000 × ^FF00001^000000";
			mes "...e entregue à eles.";
			set AE_NobleHat,1;
			close;
		}
	}
	
Step1:
	mes "^0000FF[Hietalla]^000000";
	mes "Espero que eu não esteja te incomodando com isso.";
	close;

Step2:
	if(countitem(12043) > 0){
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Aqui está, veja.";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "MAS CLARO!! Olavo, ^0000FFO Sobrevivente^000000!";
	mes "Nas batalhas, ele cozinhava para que todos tivessem força. Era o mais forte dos três!!";
	mes "Que bom que você conseguiu me lembrar! Agora já posso escrever!";
	delitem 12043,1;
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Certo, então, agora vou atrás do próximo. ^FFA500O Bondoso^000000.";
	mes " ";
	mes "^696969-Você pensa e logo lhe acende uma luz!-^000000";
	emotion e_ic,1;
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Até breve.";
	set AE_NobleHat,3;
	close;
	}
	mes "^0000FF[Hietalla]^000000";
	mes "E então, já encontrou algo sobre a história?";
	emotion e_what;
	close;

Step3:
	mes "^0000FF[Hietalla]^000000";
	mes "O próximo... qual é mesmo?";
	emotion e_hmm;
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "É ^FFA500O Bondoso^000000...";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Ah é! Está certo.";
	emotion e_heh;
	emotion e_swt,1;
	close;

Step4:
	if(countitem(1551) < 1){
		mes "^0000FF[Hietalla]^000000";
		mes "Essa história é tão emocionante.. me empolga tanto!";
		emotion e_flash;
		close;
	}
	if(countitem(2608) < 1){
		mes "^0000FF[Hietalla]^000000";
		mes "Essa história é tão emocionante.. me empolga tanto!";
		emotion e_flash;
		close;
	}
	if(countitem(7117) < 1){
		mes "^0000FF[Hietalla]^000000";
		mes "Essa história é tão emocionante.. me empolga tanto!";
		emotion e_flash;
		close;
	}
	if(countitem(523) < 3){
		mes "^0000FF[Hietalla]^000000";
		mes "Essa história é tão emocionante.. me empolga tanto!";
		emotion e_flash;
		close;
	}
	mes "^696969-Você entrega o ^FF0000Velho Grimório^696969 para Hietalla-^000000";
	delitem 7117,1;
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Óliver... ^FFA500O Bondoso^000000... e de modo engraçado, o mais culto dos três.";
	mes "Com este grimório eu poderei escrever a história! Obrigado novamente!";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Mas... e o resto dos itens sagrados?";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Fique com eles. Óliver não deve ter lhe dado em vão...";
	mes "Eu só precisarei do grimório!";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Certo, então, vou procurar o próximo e último!";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Okay, cuidado por aí.";
	set AE_NobleHat,5;
	close;

Step5:
	mes "^0000FF[Hietalla]^000000";
	mes "Cuidado por ai!";
	close;

Step6:
	mes "^0000FF[Hietalla]^000000";
	mes "Então, a mãe de Beatrice era a última mosqueteira??";
	mes "Nossa, eu nunca ia adivinhar tal descoberta...";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Agora a história está completa, graças a você!!";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Foi uma honra, senhor.";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Só não me lembro como era o lema dos três..";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Você, por um acaso, se lembra?";
	next;
	goto LemaNobleHat;
	end;

LemaNobleHat:
	mes "^696969-Diga o lema dos mosqueteiros para ^0000FFHietalla^696969-";
	input @LemaNobleHat$;
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "O lema era '^FF0000"+@LemaNobleHat$+"^000000'";
	next;
	if(@LemaNobleHat$ != "Um por todos e todos por um!"){
		mes "^0000FF[Hietalla]^000000";
		mes "Não sei.. me parece que não era exatamente isso..";
		next;
		mes "^0000FF[Hietalla]^000000";
		mes "Vamos, tente lembrar!";
		goto LemaNobleHat;
		end;
	}
	mes "^0000FF[Hietalla]^000000";
	mes "ISSO!! SIM!! EXATO!!";
	mes "Excelente, agora a história já está completa.";
	mes "E você, leve isso consigo";
	next;
	mes "^0000FF[Hietalla]^000000";
	mes "Não deixe a história ser esquecida e morrer!";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Obrigado! Nunca esquecerei.";
	getitem 5469,1;
	set AE_NobleHat,0;
	close;
}

//== • Olavo, O Sobrevivente - Quest: Chapéu de Mosqueteiro =============
prt_in,40,39,3	script	Olavo, O Sobrevivente	46,{
	if(AE_NobleHat < 1){ goto Start; }
	if(AE_NobleHat == 1){ goto Step1; }
	if(AE_NobleHat == 2){ goto Step2; }
	if(AE_NobleHat > 2) { goto Start; }
	
Start:
	if(countitem(12129) > 0){
		mes "^0000FF[Olavo]^000000";
		mes "Olá, viajante!!";
		mes "Vejo que você carrega um kit muito valioso de culinária consigo.";
		mes "Sabe, eu sou um ótimo cozinheiro, se você me deixar cozinhar com esse kit, eu te dou os melhores pratos que já se viu heh~.";
		next;
		mes "^0000FF[Olavo]^000000";
		mes "O que acha da idéia?";
		switch(select("• Tudo bem. Cozinhe!","• Melhor não, deixa pra lá.")){
			case 1:
			next;
			mes "^0000FF[Olavo]^000000";
			mes "Oh, que ótimo!";
			mes "Vou preparar os pratos já!";
			delitem 12129,1;
			next;
			mes "^0000FF[Olavo]^000000";
			mes "Já está quase pronto...";
			misceffect 608;
			next;
			mes "^0000FF[Olavo]^000000";
			mes "Ah, aqui está!";
			misceffect 492;
			next;
			mes "^0000FF[Olavo]^000000";
			mes "Espero que saboreie, estão deliciosos.";
			mes "Bom proveito!";
			getitem 12075,1;
			getitem 12080,1;
			getitem 12095,1;
			getitem 12090,1;
			getitem 12085,1;
			getitem 12100,1;
			close;
			
			case 2:
			next;
			mes "^0000FF[Olavo]^000000";
			mes "Tudo bem. Volte sempre.";
			close;
		}
	}
	mes "^0000FF[Olavo]^000000";
	mes "Olá, aventureiro.";
	close;

Step1:
	if(countitem(1004) < 1){
		mes "^0000FF[Olavo]^000000";
		mes "Olá, viajante!";
		close;
	}
	mes "^0000FF[Olavo]^000000";
	mes "Pois não, o que quer hoje?";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Vim aqui lhe entregar algo e saber de algumas coisas..";
	mes " ";
	mes "^696969-Você entrega a ^FF0000Insígnia da Cavalaria^696969 para Olavo-^000000";
	delitem 1004,1;
	next;
	mes "^0000FF[Olavo]^000000";
	mes "ARGH!!!";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Algo errado?";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "Minha cabeça doeu um pouco... algumas lembranças...";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Quero saber sobre ^0000FFO Sobrevivente^000000, ^FFA500O Bondoso^000000 e ^CD2626A Lança dos Deuses^000000...";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "...";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "Então, conhece nossa história.";
	mes "Nós eramos conhecidos como Mosqueteiros. Nossa história virou lenda quando nosso Rei se foi..";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Pois tem alguém que quer reviver sua história.";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "...";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "Então leve para este alguém:";
	mes "• ^FF0000Bife Bombástico^000000 × ^FF00001^000000";
	next;
	mes "^0000FF[Olavo]^000000";
	mes "Dos três, eu era o Mosqueteiro que cozinhava e sustentava... ele irá lembrar de mim.";
	mes "Adeus.";
	set AE_NobleHat,2;
	close;

Step2:
	mes "^0000FF[Olavo]^000000";
	mes "Não esqueça de levar:";
	mes "• ^FF0000 Bife Bombástico^000000 × ^FF00001^000000";
	mes "para aquele que está revivendo a história.";
	close;
}

//== • Óliver, O Bondoso - Quest: Chapéu de Mosqueteiro =============
yuno_pre,21,104,5	script	Óliver, O Bondoso	415,{
	if(AE_NobleHat < 3){ goto Start; }
	if(AE_NobleHat == 3){ goto Step1; }
	if(AE_NobleHat == 4){ goto Step2; }
	if(AE_NobleHat > 4){ goto Step3; }
	
Start:
	mes "^696969-Ele parece estar concentrado lendo um livro-^000000";
	close;

Step1:
	if(countitem(1004) < 1){
	mes "^0000FF[Óliver]^000000";
	mes "Oi, tudo bem?";
	emotion e_heh;
	close;
	}
	mes "^0000FF[Óliver]^000000";
	mes "'...E eram três a cavalgar...'";
	mes "^696969-O rapaz para de ler e olha para você-^000000";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "^0000FFO Sobrevivente^000000, ^FFA500O Bondoso^000000 e ^CD2626A Lança dos Deuses^000000... e você, deve ser ^FFA500O Bondoso^000000.";
	mes "Tenho algo pra você.";
	mes "^696969-Você entrega a ^FF0000Insígnia da Cavalaria^696969para Olavo-^000000";
	delitem 1004,1;
	next;
	mes "^0000FF[Óliver]^000000";
	mes "Ah sim... meus bons e velhos amigos... sinto falta deles. Todos os dias rezo por eles.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Mas o que você está fazendo num lugar como este?";
	next;
	mes "^0000FF[Óliver]^000000";
	mes "Alguém precisa purificar os corruptos.";
	mes "Vá aventureiro!! Leve consigo sempre:";
	mes "• ^FF0000 Bíblia[2] ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Rosário ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Velho Grimório ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Agua Benta ^000000×^FF0000 3^000000";
	next;
	mes "^0000FF[Óliver]^000000";
	mes "Reescreva a história, não a deixe morrer.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Tenha certeza que o farei.";
	mes "Até mais.";
	set AE_NobleHat,4;
	close;

Step2:
	mes "^696969-Ele está muito concentrado. Não vamos incomodá-lo mais. Mas lembre-se de duas palavras-";
	mes "• ^FF0000 Bíblia[2] ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Rosário ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Velho Grimório ^000000×^FF0000 1^000000";
	mes "• ^FF0000 Agua Benta ^000000×^FF0000 3^000000";
	close;

Step3:
	mes "^696969-Ele está muito concentrado. Não vamos incomodá-lo mais-^000000";
	close;
}

//== • Beatrice, A Herança - Quest: Chapéu de Mosqueteiro =============
niflheim,185,152,0	script	Beatrice, A Herança	745,{
	if(AE_NobleHat < 5){ goto Start; }
	if(AE_NobleHat == 5){ goto Step1; }
	if(AE_NobleHat > 5){ goto Step2; }
	
Start:
	mes "^696969-Você vê uma pessoa perto de alguns túmulos. Ela parece muito pensativa-^000000";
	close;

Step1:
	if(countitem(1551) < 1){
		mes "^696969-Você vê uma pessoa perto de alguns túmulos. Ela parece muito pensativa-^000000";
		close;
	}
	if(countitem(2608) < 1){
		mes "^696969-Você vê uma pessoa perto de alguns túmulos. Ela parece muito pensativa-^000000";
		close;
	}
	if(countitem(523) < 3){
		mes "^696969-Você vê uma pessoa perto de alguns túmulos. Ela parece muito pensativa-^000000";
		close;
	}
	mes "^696969-Passando por ali, você vê uma pessoa perto de alguns túmulos-^000000";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Com licença! Você sabe algo sobre ^CD2626A Lança dos Deuses^000000?";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Olhe para baixo e encontrará.";
	next;
	mes "^696969-Você fica confuso e olha para os túmulos com uma expressão assustada-^000000";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Não, você não está pensando errado... ^CD2626A Lança dos Deuses^000000 foi enterrada aqui.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Quem é você?!";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Eu sou Beatrice D'Belmont. Filha de Li D'Belmont, ^CD2626A Lança dos Deuses^000000.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Então, o último mosqueteiro era uma mulher?";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Sim, a líder. A portadora do chapéu vermelho.";
	mes "Morreu em batalha e foi enterrada as pressas.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Meus pêsames. Tome, não é grande coisa mas ajudará em algo.";
	mes " ";
	mes "^696969-Você entrega o ^FF0000Rosário^696969, as três ^FF0000Água Benta^696969 e a ^FF0000Bíblia^696969 para Beatrice-^000000";
	delitem 1551,1;
	delitem 2608,1;
	delitem 523,3;
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Obrigada, jovem.";
	mes "Mas como sabe dessa história, se a muito tempo ela se perdeu?";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Eu queria reconstruir a história deles, mas... já que Li está morta, não tem como.";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Eram os Três Mosqueteiros, Guardavam o Rei e a Realeza. Eram os únicos que tinha o código mais honroso da história.";
	mes "Porém, quando o Rei morreu, tiveram que se separar.";
	mes "E depois que minha mãe morreu, eles perderam contato.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Fico agradecido pela história, e meus pêsames. Quem está reescrevendo sua história é Hietalla.";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Eu o conheço... cuidou de mim sem saber quem eu sou, mas sempre me via vindo para esses lados.";
	mes "Volte até ele, reescreva a história e não a deixe morrer.";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Faça com que o povo reconheça os heróis! Agora, a alma do último mosqueteiro descansará em paz.";
	next;
	mes "^BF3EFF["+strcharinfo(0)+"]^000000";
	mes "Obrigado e fique bem.";
	next;
	mes "^FF0000[Beatrice]^000000";
	mes "Igualmente.";
	set AE_NobleHat,6;
	close;

Step2:
	mes "^696969-Você vê uma pessoa perto de alguns túmulos. Ela parece muito pensativa, mas agora, feliz-^000000";
	close;
}