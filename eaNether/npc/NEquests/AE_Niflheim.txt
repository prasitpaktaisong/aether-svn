//== Aether Script ===========================================
// • NPCs de Niflhelm
//===== Por: =================================================
// • Haziel
//===== Versão Atual: ========================================
// • 1.0
//===== Descrição: ===========================================
// • NPCs Aether - Cidade de Niflhelm
//===== Changelog: ===========================================
// • 1.0 - Adicionado Dewey - Máscara do Fugitivo (20/05/2010)
// • 1.1 - Adicionado Lyner - Máscara do Fugitivo (20/05/2010)
//============================================================

//== • Dewey - Quest: Máscara do Fugitivo ====================
niflheim,171,153,5	script	Dewey, O Poltergeist#nif	1506,{
	if(AE_Fugitivo > 0){
		mes "^0000FF[Dewey]^000000";
		mes "Obrigado novamente pelo gesto... estou bem melhor agora.";
		mes "Nessa forma, não posso te agradecer. Porém, vá até ^FF0000Yuno^000000 e procure por ^FF0000Alexia^000000.";
		mes "Leve uma ^FF0000Carta Mascarado^000000, isso me permitirá usar os poderes dela.";
		mes "Com a ajuda dela, poderei te recompensar.";
		close;
	}
	mes "^0000FF[Dewey]^000000";
	mes "A morte é fria como a mais gélida das noites...";
	if(countitem(10019) >= 5){
		mes "Este cachecol é fino, não me esquenta...";
		mes "Pode me dar esses ^FF0000Panos Vermelhos^000000 que você carrega?";
		if(select("• Sim, claro.:• Não, me desculpe.") == 2) close;
		next;

		delitem 10019, 5;
		mes "^0000FF[Dewey]^000000";
		mes "Obrigado...";
		mes "Eu... eu não sou como os outros ^FF0000Mascarados^000000. Eles perderam suas consciências e não lembram quem são, mas eu me lembro...";
		mes "Eu me lembro...";
		next;
		
		mes "^0000FF[Dewey]^000000";
		mes "Obrigado novamente pelo gesto... estou bem melhor agora.";
		mes "Nessa forma, não posso te agradecer. Porém, vá até ^FF0000Yuno^000000 e procure por ^FF0000Alexia^000000.";
		mes "Leve uma ^FF0000Carta Mascarado^000000, isso me permitirá usar os poderes dela.";
		mes "Com a ajuda dela, poderei te recompensar.";
		set AE_Fugitivo, 1;
	}
	close;
}

//== • Lyner - Quest: Rubor de Batalha =======================
nif_in,89,15,3	script	Lyner#nif	998,{
	If (AE_Rubor > 8){
		mes "^A71ADB[Lyner]^000000";
		mes "Infelizmente, não há muitos vivos que ainda conhecem o ^FF0000Rubor de Batalha^000000.";
		mes "Então, você deve procurar alguém que não pertence à vida, nem à morte.";
		next;

		mes "^A71ADB[Lyner]^000000";
		mes "Siga para a ilha de ^0000FFZorya^000000 e procure o eco daquele que já chamou-se ^0000FFBaylian^000000, o que insiste em permanecer no mundo dos que vivem.";
		close;
	}

	If (AE_Rubor == 8){
		if ((countitem(7829)>=10) && (countitem(7828)>=10)){ 	
			mes "^A71ADB[Lyner]^000000";		
			mes "Belas condecorações, ficarei com elas.";
			delitem 7829,10;
			delitem 7828,10;
			mes "Enfim, provou quem és.";
			next;
		
			mes "^A71ADB[Lyner]^000000";
			mes "Infelizmente, não há muitos vivos que ainda conhecem o ^FF0000Rubor de Batalha^000000.";
			mes "Então, você deve procurar alguém que não pertence à vida, nem à morte.";
			next;

			mes "^A71ADB[Lyner]^000000";
			mes "Siga para a ilha de ^0000FFZorya^000000 e procure o eco daquele que já chamou-se ^0000FFBaylian^000000, o que insiste em permanecer no mundo dos que vivem.";
			set AE_Rubor,9;
			close;
		} else {
			mes "^A71ADB[Lyner]^000000";
			mes "Traga o que lhe pedi.";
			mes "^0000FF10 Emblemas de Bravura^000000";
			mes "^0000FF10 Emblemas de Valor^000000";
			mes "Não se esqueça...";
			close;
		}
	}
	
	If (AE_Rubor < 7){
		mes "^A71ADB[Lyner]^000000";
		mes "Silêncio... por favor.";
		close;
	}
	
	mes "^A71ADB[Lyner]^000000";
	mes "Silêncio... por favor.";
	if (countitem(5040)>=10){
		switch(select("• Mostrar-lhe as ^FF0000Canduras^000000.","• Manter o silêncio.")){
			case 1:
				next;
				mes "^A71ADB[Lyner]^000000";
				mes "Oh, vejo que conhece ^0EC400Harold^000000...";
				mes "^0000FF*Rapidamente, as ^FF0000Canduras^0000FF se desfazem e somem*";
				delitem 5040,10;
				mes "^000000Não é o primeiro que procura o ^FF0000Rubor de Batalha^000000...";
				next;
		
				mes "^A71ADB[Lyner]^000000";
				mes "Só posso indicar isso a um guerreiro experiente.";
				mes "Vá ao ^FF0000Campo de Batalha^000000 e me traga suas condecorações:";
				mes "^0000FF10 Emblemas de Valor^000000";
				mes "^0000FF10 Emblemas de Bravura^000000";
				next;
	
				mes "^A71ADB[Lyner]^000000";
				mes "Aguardarei-te aqui, se conseguir, é claro.";
				set AE_Rubor,8;
				close;
			case 2:
				close;
		}
	}
	close;
}
