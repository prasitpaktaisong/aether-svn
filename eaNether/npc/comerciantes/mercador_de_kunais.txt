//===== Cronus Script ========================================
//= Mercador de Kunai @ que_ng
//===== Por: =================================================
//= eAthena dev team
//===== Versão Atual: ========================================
//= 1.2c
//===== Compatível Com: ======================================
//= eAthena/Cronus
//===== Descrição: ===========================================
//= Troca Shurikens e Pedras Ninja por Kunais Elementais
//===== Comentários: =========================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=	  id of the 2 required items plus the amount. Can trade
//= 	  up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.2b Traduzido. [Pudim]
//= 1.2c Revisado. [RoM]
//============================================================

que_ng,72,29,3	script	Mercador de Kunai Kashin	83,{

	mes "[Kashin]";
	if(BaseJob != Job_Ninja) {
		mes "Eu sou Kashin do Vento. Sou o comerciante das sombras.";
		next;
		mes "[Kashin]";
		mes "Não me parece que você seja um Ninja... Apenas dê uma boa olhada ao redor e siga o seu caminho.";
		close;
	}
	mes "Eu sou Kashin!";
	mes "Se você sempre tem poucas Shurikens, venha me ver.";
	next;
	mes "[Kashin]";
	mes "Qual vai ser?";
	mes "Escolha o que você deseja.";
	next;

	switch(select("Kunai Veneno Mortal:Kunai Nevasca:Kunai Sopro do Vento:Kunai Terra Negra:Kunai Onda de Calor:Cancelar")) {
	//usage: callfunc "Kunai_Trade",itemreqid1,itemreqct1,itemreqid2,itemreqct2,itemidtrade;
	case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
	case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
	case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
	case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
	case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
	default:
		mes "[Kashin]";
		mes "Hmm~ Certo~";
		mes "Volte sempre~";
		mes "Eu, Kashin do Vento, sempre estarei neste lugar.";
		close;
	}
	close;
}

function	script	Kunai_Trade	{
	
	mes "[Kashin]";
	mes "Se você me der "+getarg(1)+" "+getitemname(getarg(0))+" e "+getarg(3)+" "+getitemname(getarg(2))+", eu lhe darei um pacote com 10 "+getitemname(getarg(4))+".";
	next;
	mes "[Kashin]";
	mes "Você pode comprar apenas 500 pacotes por vez.";
	mes "Se você não quer comprar, apenas digite 0 na quantidade.";
	next;
	input .@amount;

	mes "[Kashin]";
	if(.@amount < 1) {
		mes "[Kashin]";
		mes "Hmm~ Certo~";
		mes "Volte sempre !";
		mes "Eu, Kashin do Vento, sempre estarei neste lugar.";
		close;
	}
	if(.@amount > 500) {
		mes "Você excedeu a quantidade!";
		mes "Digite um número válido da próxima vez!";
		close;
	}
	if(countitem(getarg(0)) < .@amount*getarg(1) || countitem(getarg(2)) < .@amount*getarg(3)) {
		mes "Hmm... isto não é bom~";
		mes "Você não possui material suficiente para fazer a quantidade de Kunais que deseja.";
		mes "Traga um pouco mais se você quer esta quantidade.";
		close;
 	}
	if(checkweight(getarg(4), .@amount*10) == 0) {
		mes "Sua mochila esta muito cheia para carregar estes itens. Volte após esvaziá-la um pouco.";
		close;
	}
	mes "Certo~ Muito bem!";
	mes "Quantidade verificada!";
	mes "Aqui estão seus itens.";
	delitem getarg(0),getarg(1)*.@amount;
	delitem getarg(2),getarg(3)*.@amount;
	getitem getarg(4),10*.@amount;
	close;
}